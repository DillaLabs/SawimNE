apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // The Fabric Gradle plugin uses an open ended version to react
        // quickly to Android tooling updates
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation 'com.github.ge0rg:MemorizingTrustManager:b6a3d558e4'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.github.anrwatchdog:anrwatchdog:1.2.+'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'

    implementation 'com.github.bumptech.glide:glide:3.7.0'

    implementation 'com.squareup.okhttp3:okhttp:3.2.0'

    implementation 'com.google.android.gms:play-services-gcm:9.2.1'

    //debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    //releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "ru.sawim.jp"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 83
        versionName '1.3.8b'
    }
    lintOptions {
        abortOnError true
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }
    // signing config
    if (file("keystore.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("keystore.properties")))

        signingConfigs {
            sawim {
                storeFile file(props["key.store"])
                storePassword props["key.store.password"]
                keyAlias props["key.alias"]
                keyPassword props["key.alias.password"]
            }
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-android.txt'
                signingConfig signingConfigs.sawim
            }
        }
    }
    applicationVariants.all { variant ->
        println "*********" + variant.description + "**********";
        def apkName = "SawimNE.apk";
        variant.outputs.all { output ->
            outputFileName = new File(apkName)
        }
    }
}
